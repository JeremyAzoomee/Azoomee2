buildscript {
    repositories {
        jcenter()
        mavenLocal()
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }
    dependencies {
        // The Fabric Gradle plugin uses an open ended version to
        // react quickly to Android tooling updates
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.tinizine.azoomee"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 999000
        versionName '9.9.9'
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        jniLibs.srcDir "libs"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "assets"
    }

    signingConfigs {

       release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
    }
}

repositories {
	mavenLocal()
    maven {
        url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2'
    }
    maven { url 'https://maven.fabric.io/public' }

}

crashlytics {
    enableNdk true
    androidNdkOut 'obj'
    androidNdkLibsOut 'libs'
}

play {
    track = 'beta'
    serviceAccountEmail = 'app-uploader@api-8652937103049796797-14532.iam.gserviceaccount.com'
    pk12File = file('../../../android_signing_key/appuploader.p12')
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'jars')
    compile project(':libcocos2dx')
    compile project(':azoomee_common')
    compile 'com.mixpanel.android:mixpanel-android:5.+'
    compile 'com.google.android.gms:play-services-base:11.2.2'
    compile 'com.google.android.gms:play-services-ads:11.2.2'
    // Crashlytics Kit
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true
    }
    // NDK Kit
    compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.5@aar') {
        transitive = true
    }
    compile 'com.appsflyer:af-android-sdk:4+@aar'

    // Urban Airship SDK
    compile 'com.urbanairship.android:urbanairship-sdk:8.8.+'
    compile 'com.google.android.gms:play-services-gcm:11.2.2'
    compile 'com.android.support:support-v4:26.0.2'

    // Recommended for in-app messaging
    compile 'com.android.support:cardview-v7:26.0.2'
}

task cleanWebcommApi(type: Delete) {
    delete '../../Resources/res/webcommApi'
}

task copyWebcommApi(type: Copy) {
    from '../../../webgameapi_repo/webcommApi'
    into '../../Resources/res/webcommApi'

}

task ctaZip(type: Zip) {
    from fileTree(dir: '../../../app-content-dynamicnode/android/')
    archiveName 'CTAFiles.zip'
    destinationDir(file('../../Resources/res/cta_assets/'))
}

task cleanAssets(type: Delete) {
    delete 'assets'
}
task copyAssets(type: Copy) {
    from '../../Resources'
    into 'assets'
}

copyAssets.mustRunAfter copyWebcommApi
copyAssets.mustRunAfter ctaZip
clean.dependsOn cleanWebcommApi
clean.dependsOn cleanAssets
preBuild.dependsOn copyWebcommApi
preBuild.dependsOn ctaZip
preBuild.dependsOn copyAssets
